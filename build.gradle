buildscript {
  apply from: "config/index.gradle"

  CONFIG.versions.kotlin = '1.3.50'
  CONFIG.versions.okhttp = '3.12.3' // Higher versions require Java 8 to be enabled

  repositories {
    google()
    mavenCentral()
    jcenter()
    gradle()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$CONFIG.versions.android.plugin"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$CONFIG.versions.kotlin"
    classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.2'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.1.1'
}

allprojects { proj ->
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  apply plugin: 'pl.allegro.tech.build.axion-release'
  scmVersion {
    useHighestVersion = true
    tag {
      prefix = 'v'
      versionSeparator = ''
      versionIncrementer 'incrementMinor'
    }
  }

  proj.version = scmVersion.version
}
