apply from: '../config/android/library.gradle'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'

android {
  sourceSets.each {
    it.java.srcDirs += "src/$it.name/kotlin"
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$CONFIG.versions.kotlin"
  implementation "com.squareup.okhttp3:okhttp:$CONFIG.versions.okhttp"
  implementation "com.google.code.gson:gson:$CONFIG.versions.thirdParty.gson"
  compileOnly 'androidx.annotation:annotation:1.0.0'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.robolectric:robolectric:4.3'
  testImplementation "org.amshove.kluent:kluent-android:1.65"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.mockito:mockito-inline:3.2.0"
  testImplementation "com.squareup.okhttp3:mockwebserver:$CONFIG.versions.okhttp"
  api 'androidx.test:core:1.4.0'
}

apply from: "../config/quality/jacoco/android.gradle"
jacoco {
  toolVersion = "0.8.5"
}

apply from: "../config/quality/checkstyle/android.gradle"
apply from: "../config/quality/detekt/android.gradle"
dependencies {
  // Enable the KtLint rules
  detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.16.0"
}

apply from: '../config/publish/android.gradle'
afterEvaluate {
  publishing {
    publications {
      SdkUtils(MavenPublication, androidArtifact())
    }
  }
}

def isSnapshot = project.version.contains('-')
if (isSnapshot) {
  ext["MAVEN_CENTRAL_IS_SNAPSHOT"] = true
}
apply from: '../config/publish/maven-central.gradle'
