apply from: '../config/android/library.gradle'
apply from: '../config/quality/gitleaks/build.gradle'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'

android {
  def property = { key ->
    return System.getenv(key) ?: (project.hasProperty(key) ? project."$key" : "")
  }

  defaultConfig {
    consumerProguardFiles "proguard-rules.pro"
    resValue 'string', 'sdk_utils__version', project.version

    buildConfigField("String", "EVENT_LOGGER_API_URL_PRD", "\"${property("EVENT_LOGGER_API_URL_PRD")}\"")
    buildConfigField("String", "EVENT_LOGGER_API_URL_STG", "\"${property("EVENT_LOGGER_API_URL_STG")}\"")
    buildConfigField("String", "EVENT_LOGGER_API_KEY_PRD", "\"${property("EVENT_LOGGER_API_KEY_PRD")}\"")
    buildConfigField("String", "EVENT_LOGGER_API_KEY_STG", "\"${property("EVENT_LOGGER_API_KEY_STG")}\"")
  }

  lintOptions {
    disable 'UnusedResources'
  }

  sourceSets.each {
    it.java.srcDirs += "src/$it.name/kotlin"
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  kotlinOptions {
    jvmTarget = JavaVersion.toVersion(CONFIG.versions.java).toString()
  }

  buildTypes {
    debug {
      buildConfigField("String", "EVENT_LOGGER_API_URL", "\"${property("EVENT_LOGGER_API_URL_STG")}\"")
      buildConfigField("String", "EVENT_LOGGER_API_KEY", "\"${property("EVENT_LOGGER_API_KEY_STG")}\"")
    }

    release {
      buildConfigField("String", "EVENT_LOGGER_API_URL", "\"${property("EVENT_LOGGER_API_URL_PRD")}\"")
      buildConfigField("String", "EVENT_LOGGER_API_KEY", "\"${property("EVENT_LOGGER_API_KEY_PRD")}\"")
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$CONFIG.versions.kotlin"
  implementation "com.squareup.okhttp3:okhttp:$CONFIG.versions.okhttp"
  compileOnly 'androidx.annotation:annotation:1.0.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
  implementation 'com.squareup.retrofit2:retrofit:2.9.0'
  implementation 'com.google.code.gson:gson:2.8.9'
  implementation 'com.google.android.gms:play-services-base:17.0.0'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.robolectric:robolectric:4.7.3'
  testImplementation "org.amshove.kluent:kluent-android:1.65"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.mockito:mockito-inline:3.2.0"
  testImplementation "com.squareup.okhttp3:mockwebserver:$CONFIG.versions.okhttp"
  testImplementation 'androidx.test:core:1.4.0'
}

apply from: "../config/quality/jacoco/android.gradle"
jacoco {
  toolVersion = "0.8.7"
}

apply from: "../config/quality/checkstyle/android.gradle"
apply from: "../config/quality/detekt/android.gradle"
dependencies {
  // Enable the KtLint rules
  detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.20.0"
}

detekt {
  config = rootProject.files('detekt-config.yml')
}

apply from: '../config/publish/android.gradle'
afterEvaluate {
  publishing {
    publications {
      SdkUtils(MavenPublication, androidArtifact())
    }
  }
}


/*
//publish snapshot to maven central
def isSnapshot = project.version.contains('-')
if (isSnapshot) {
  ext["MAVEN_CENTRAL_IS_SNAPSHOT"] = true
}
apply from: '../config/publish/maven-central.gradle'*/

def isSnapshot = project.version.contains('-')
if (isSnapshot) {
  project.ext.ARTIFACTORY_URL=CONFIG.property("ARTIFACTORY_URL_SNAPSHOT")
  project.ext.ARTIFACTORY_REPO=CONFIG.property("ARTIFACTORY_REPO_SNAPSHOT")
  apply from: '../config/publish/repository.gradle'
} else {
  apply from: '../config/publish/maven-central.gradle'
}